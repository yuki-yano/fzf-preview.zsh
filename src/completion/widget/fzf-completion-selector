#autoload

setopt local_options BASH_REMATCH

local command=$1
local symbol_command="fzf-preview-symbol-${command}"
local user_symbol_command="symbol-${command}"

local -A symbols
local symbol regex

if whence $symbol_command > /dev/null 2>&1; then
  $symbol_command
fi

if whence $user_symbol_command > /dev/null 2>&1; then
  $user_symbol_command
fi

for regex in "${(@k)symbols}"; do
  local -a BASH_REMATCH=()
  [[ $LBUFFER =~ $regex ]]
  if [[ ${BASH_REMATCH[1]} != "" ]]; then
    symbol=$symbols[$regex]
  fi
done

if [[ $symbol != "" ]]; then
  local completion_function="fzf-preview-command-${symbol}"
  local user_completion_function="command-${symbol}"

  if whence $completion_function > /dev/null 2>&1; then
    $completion_function
  fi

  if whence $user_completion_function > /dev/null 2>&1; then
    $user_completion_function
  fi
else
  zle expand-or-complete
fi
